    buildscript {
        ext {
            springBootVersion = '3.2.3'
        }
        repositories {
            mavenCentral()
        }
        dependencies {
            classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
            classpath "io.spring.gradle:dependency-management-plugin:1.1.4"
        }
    }

    // 하위 모든 프로젝트 공통 세팅
    subprojects {
        apply plugin: 'java'
        apply plugin: 'idea'
        apply plugin: 'org.springframework.boot'
        apply plugin: 'io.spring.dependency-management'
        apply plugin: 'maven-publish'

        sourceCompatibility = '17'
        targetCompatibility = '17'
        compileJava.options.encoding = 'UTF-8'

        repositories {
            mavenCentral()
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/smk692/boilerplate-library"

                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("MY_GITHUB_USERNAME")
                    password = project.findProperty("gpr.token") ?: System.getenv("MY_GITHUB_TOKEN")
                }
            }
        }

        // 하위 모듈에서 공통으로 사용하는 세팅 추가
        dependencies {
            compileOnly 'org.projectlombok:lombok'

            annotationProcessor 'org.projectlombok:lombok'
            annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

            testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
            testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
        }

        test {
            useJUnitPlatform()
        }
    }

    project(':module-1') {
        // 공통 코드
        apply plugin: 'java'

        bootJar { enabled = false }
        jar { enabled = true }

        dependencies {
        }

        // 배포
        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from components.java

                    groupId = 'com.example'
                    artifactId = 'module-1'
                    version = '1.0.0'

                    pom {
                        name = 'Module 1'
                        description = 'Module 1는 특정 기능을 제공하는 라이브러리입니다.'
                        url = 'https://github.com/smk692/boilerplate-library'
                    }
                }
            }
        }


    }

    project(':module-2') {
        // 공통 코드
        apply plugin: 'java'

        bootJar { enabled = false }
        jar { enabled = true }

        dependencies {
        }

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from components.java

                    groupId = 'com.example'
                    artifactId = 'module-2'
                    version = '1.0.0'

                    pom {
                        name = 'Module 2'
                        description = 'Module 2는 특정 기능을 제공하는 라이브러리입니다.'
                        url = 'https://github.com/smk692/boilerplate-library'

                        licenses {
                            license {
                                name = 'Apache License, Version 2.0'
                                url = 'http://www.apache.org/licenses/LICENSE-2.0'
                            }
                        }
                        scm {
                            url = 'https://github.com/smk692/boilerplate-library'
                        }
                    }
                }
            }
        }
    }


